plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

// 使用插件
apply plugin: "maven-publish"

// 上传源码
task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = "sources"
}

afterEvaluate {
    publishing {
        publications {
            // 发布debug包
            debug(MavenPublication) {
                // 使用 Android Gradle 插件生成的组件，作为发布的内容
                from components.debug
                // 配置aar的参数
                groupId = "com.youer.ui"
                artifactId = "floatwindow"
                version = "1.0.0.1-SNAPSHOT"
                // 上传source
                artifact sourceJar
            }
            // 发布release包
            release(MavenPublication) {
                from components.release
                // 配置aar的参数
                groupId = "com.youer.ui"
                artifactId = "floatwindow"
                version = "1.0.0"
                // 上传source
                artifact sourceJar
            }
        }
        repositories { RepositoryHandler handler->
            handler.mavenLocal()  // 发布到默认的 本地maven 仓库
        }
    }
}
